buildscript {
    ext.kotlin_version = '1.4.21'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'cz.craftmania.craftpack'
version '1.3.7'

sourceCompatibility = 11
targetCompatibility = 11

// Nastaveni UTF
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

clean { delete "./build" }

repositories {
    mavenCentral()
    maven { url "https://packages.craftmania.cz/repository/craftmania/" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
}

dependencies {
    compileOnly ('org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly 'cz.craftmania.craftcore:craftcore:2.4.0'
}

task compilePlugin(type: Jar) {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            name = "craftpack"
            url = uri("https://maven.pkg.github.com/craftmania-cz/craftpack")
            credentials {
                username = GPR_USER
                password = GPR_TOKEN
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
